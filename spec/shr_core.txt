Grammar:		DataElement 5.0
Namespace:		shr.core
Description:	"The SHR Core domain contains definitions for a variety of general-purpose elements that are used widely in SHR. These include definitions for coding, expressions of time, quantities, addresses, names, and more."


Path:			FHIR = http://hl7.org/fhir/ValueSet
// CodeSystem:     CC = http://www.nationsonline.org/oneworld/country_code_list
CodeSystem:     LNC = http://loinc.org
CodeSystem:     SCT = http://snomed.info/sct
CodeSystem:     MTH = http://ncimeta.nci.nih.gov
CodeSystem:		NCI = https://evs.nci.nih.gov/ftp1/CDISC/SDTM/
CodeSystem:     UCUM = http://unitsofmeasure.org 


//------------ Complex Types aligned to FHIR ---------------

Element:		Quantity
Description:	"A quantity with units, whose value may be bounded from above or below, as defined in FHIR"
Value:			0..1 decimal
0..1			Comparator
0..1			Units

		Element:		Comparator
		Value:			code from http://hl7.org/fhir/ValueSet/quantity-comparator

		Element:		Units
		Concept:		TBD
		Description:	"Code for the unit of measure of the quantity."
		Value:			Coding should be from AllUnitsVS

Element:		SimpleQuantity
Based on:		Quantity
Description:	"A quantity where the comparator is not used, as defined in FHIR"
0..0			Comparator

Element:		IntegerQuantity
Based on:		Quantity
Description:	"A Quantity that is an integer."
				Units.Coding is UCUM#1

Element:		Age
Based on:		Quantity
Concept:		MTH#C0001779
Description:	"How long someone has been alive, or something has existed."
				Units with Coding from http://hl7.org/fhir/ValueSet/units-of-time

Element:		Distance
Based on:		Quantity
Concept:		MTH#C0012751
Description:	"The measure of space separating two objects or points."
				Units with Coding from UnitsOfLengthVS

Element:		Duration
Based on:		SimpleQuantity
Concept:		MTH#C0449238
Description:	"The length of time that something continues."
				Units with Coding from TimeUnitOfMeasureVS

Element:		Money
Based on:		Quantity
Concept:		TBD
Description:	"Representation of an amount of currency or monetary value."
				Units with Coding from CurrencyVS


//------------- Complex data types in FHIR 3.0 ---------------

Element:		EmbeddedContent
Concept:		TBD
Description:	"Abstract class defining the common metadata of all types of encapsulated data, such as images."

Element:		Media  // maps to Attachment
Based on:		EmbeddedContent
Concept:		TBD
Description:	"A file that contains audio, video, image, or similar content."
0..1			BinaryData
0..1			ContentType
0..1			Language
0..1			ResourceLocation
0..1			ResourceSize
0..1			Hash
0..1			Title
0..1			CreationTime

	Element:		BinaryData
	Concept:		TBD
	Description:	"The data itself."
	Value:			base64Binary

	Element:		ContentType
	Concept:		TBD
	Description:	"Mime type of the content, with charset etc."
	Value:			code
	
	Element:		Language
	Concept:		MTH#C0023008
	Description: 	"A human language, spoken or written."
	Value:			CodeableConcept from http://hl7.org/fhir/ValueSet/languages if covered

	Element:		ResourceLocation
	Concept:		TBD
	Description:	"URI where data can be found."
	Value:			uri

	Element:		ResourceSize
	Concept:		TBD
	Description:	"Number of bytes of content in the resource (if url provided)"
	Value:			unsignedInt

	Element:		Hash
	Concept:		TBD
	Description:	"A hash code of the data (sha-1, base64ed)"
	Value:			base64Binary
	
	Element:   	 	CreationTime  
	Concept:        MTH#C3669169
	Description:    "The point in time when the information was recorded in the system of record."
	Value:          dateTime  // we can map to instant from dateTime

	Element:		Title
	Concept:		TBD
	Description:	"A distinguishing word or group of words naming an item."
	Value:			string


//---- Coding departs from CIMI to align with FHIR

Element:		Coding
Concept:		MTH#C0805701
Description:	"Coding of a concept, drawn from a controlled vocabulary. Includes the vocabulary and version, if applicable. May include a display text, and a descriptor expressing the intended interpretation of the code."
Value:			0..1 code  // changed to 0..1 so SHR won't generate a profile.
0..1			CodeSystem
0..1 			CodeSystemVersion
0..1			DisplayText
//0..*			CodingQualifier
	
/*
 TODO: Suggest changing FHIR's Coding.userSelected to Coding.qualifier 
	Element:		CodingQualifier
	Concept:		TBD
	Description:	"A description of the purpose or origin of the code that assists in understanding the code."
	Value:			code from CodingQualifierVS
*/

	Element:		CodeSystem
	Concept:		TBD
	Description:	"A formal terminology system."
	Value:			uri

	Element:		CodeSystemVersion
	Concept:		TBD
	Description:	"The version of the vocabulary being used, if applicable."
	Value:			string // Should be id type in FHIR, but it is a string (no issue recorded yet)
	
	Element:		DisplayText
	Concept:		MTH#C1548311
	Description:	"A string meant for reading by a person, for example, accompanying a code."
	Value:			string


Element:		CodeableConcept
Concept:		TBD
Description:	"A concept represented by a code or codes."
0..*			Coding
0..1			DisplayText


Element:		Range
Concept:		TBD
Description:	"An interval defined by a quantitative upper and/or lower bound. One of the two bounds must be specified, and the lower bound must be less than the upper bound. When Quantities are specified, the units of measure must be the same."
0..1			LowerBound
0..1			UpperBound

	Element:		LowerBound
	Concept:		TBD
	Description:	"The lower limit on a range"
	Value:			SimpleQuantity  // or decimal or unsignedInt or integer 

	Element:		UpperBound
	Concept:		TBD
	Description:	"The upper limit on a quantitative value."
	Value:			SimpleQuantity  // or decimal or unsignedInt or integer 

	
Element:		Ratio
Concept:		MTH#C0456603
Description:	"A unit of measurement for the quotient of the amount of one entity to another."
1..1			Numerator
1..1			Denominator

	Element:		Numerator
	Concept:		MTH#C2825219
	Description:	"The dividend of a fraction."
	Value:			Quantity

	Element:		Denominator
	Concept:		MTH#C2825218
	Description:	"The divisor of a fraction."
	Value:			Quantity

Element:   		ContactPoint
Concept:		MTH#C2986441
Description:	"An electronic means of contacting an organization or individual."		
1..1			TelecomNumberOrAddress
1..1			Type from http://hl7.org/fhir/ValueSet/contact-point-system
0..1			Purpose from http://hl7.org/fhir/ValueSet/contact-point-use
0..1			PriorityRank
0..1			EffectiveTimePeriod

		Element:		TelecomNumberOrAddress
		Concept:		TBD
		Description:	"A user name or other identifier on a telecommunication network, such as a telephone number (including country code and extension, if necessary), email address, or SkypeID."
		Value:			string	
		
		Element:		Type
		Concept:		MTH#C0332307
		Description:	"The most specific code (lowest level term) describing the kind or sort of thing being represented." 
		Value:			CodeableConcept
		
		Element:		Purpose
		Description:	"Why the item is used."
		Value:			Coding

		Element: 		PriorityRank
		Concept:		MTH#C0549179
		Description:	"An indication of the importance of an action."
		Value:			positiveInt or CodeableConcept

		Element:		EffectiveTimePeriod
		Based on:		TimePeriod
		Concept:		MTH#C2985763
		Description:	"The date and time span for which something is active, valid, or in force."





/* TODO: Additional MetaData Types: see http://hl7.org/fhir/metadatatypes.html
Contributor
DataRequirement
ParameterDefinition
RelatedArtifact
TriggerDefinition
UseageContext
*/


Element:		TimePeriod
Concept:		MTH#C1948053
Description:	"A period of time defined by a start and end time, date, or year. 
If the start element is missing, the start of the period is not known. If the end element is missing, it means that the period is ongoing, or the start may be in the past, and the end date in the future, which means that period is expected/planned to end at the specified time. The end value includes any matching date/time. For example, the period 2011-05-23 to 2011-05-27 includes all the times from the start of the 23rd May through to the end of the 27th of May."
0..1			TimePeriodStart
0..1			TimePeriodEnd  // lack of end time indicates the period was continuing at the time the entry was created

	Element: 		TimePeriodStart    // synonym "EffectiveDate"
	Concept: 		MTH#C1301880
	Description: 	"The time at which something is to take effect, start, or did start. Boundary is considered inclusive."
	Value:			dateTime   // TODO: would like to have date or dateTime but that wouldn't be FHIR-compatible

	Element:		TimePeriodEnd
	Concept:		MTH#C1522314
	Description:	"The time at which something is to end or did end. Boundary is considered inclusive."
	Value:			dateTime   // TODO: would like to have date or dateTime but that wouldn't be FHIR-compatible


Element: 	Timing
Concept:	TBD
Description: "A timing schedule that specifies an event that may occur multiple times. 
Timing offers a choice of multiple OccurrenceTime (used is used to specify specific times), or recurrence patterns."
0..*		OccurrenceTime
0..1		TimingCode
0..1		EventDuration
0..1		RecurrencePattern
0..1		RecurrenceRange

	Element:		OccurrenceTimeOrPeriod
	Concept:		TBD
	Description:	"The point in time or span of time in which something happens."
	Value:			date or dateTime or TimePeriod

	Element:		OccurrenceTime
	Based on:		OccurrenceTimeOrPeriod
	Concept:		TBD
	Description:	"The point in time in which something happens."
	Value:			dateTime

	Element:		TimingCode
	Concept:		TBD
	Description: 	"A code for the timing schedule. 
	Some codes such as BID are ubiquitous, but many institutions define their own additional codes. If a code is provided, the code is understood to be a complete statement of whatever is specified in the structured timing data, and either the code or the data may be used to interpret the Timing, with the exception that .repeat.bounds still applies over the code (and is not contained in the code)."
	Value:			CodeableConcept from http://hl7.org/fhir/ValueSet/timing-abbreviation if covered
	
	Element:		EventDuration
	Description:	"The length of the recurring event."
	Value:			DurationRange
					
				Element:		DurationRange
				Based on:		Range
				Description:	"A range of durations."
				LowerBound value is type Duration
				UpperBound value is type Duration

	Element:	RecurrencePattern
	Concept:	TBD
	Description: "A set of rules that describe when a recurring event is scheduled."
	0..1		RecurrenceInterval
	0..*		DayOfWeek
	0..*		TimeOfDay
	0..*		DailyLifeEvent
	0..1		LifeEventOffset
	0..1		CountPerInterval

		Element:		RecurrenceInterval
		Concept:		TBD
		Description:	"The period of time after which the pattern repeats, for example, each day. 
		To specify an event should take place every other Monday, the recurrence interval should be two weeks, and DayOfWeek should be Monday."
		Value:			Duration
		
		Element:		DayOfWeek
		Concept:		TBD
		Description:	"A day of the week that the pattern should take place."
		Value:			code from http://hl7.org/fhir/ValueSet/days-of-week
		
		Element:		TimeOfDay
		Concept:		TBD
		Description:	"Time of day the event should take place on the designated day(s). 
		TimeOfDay should only be specified if the RecurrenceInterval and/or DayOfWeek establishes the day when the event should take place."
		Value:			time
		
		Element:		DailyLifeEvent
		Concept:		TBD
		Description:	"A quotidian landmark, such as rising, mealtime, or bedtime, when an event should take place."
		Value:			code from http://hl7.org/fhir/ValueSet/event-timing

		Element:		LifeEventOffset
		Concept:		TBD
		Description:	"A time in minutes before or after a given life event, for example, 30 minutes before a meal. Whether this means before or after is carried by the life event code."
		Value:			unsignedInt
		
		Element:		CountPerInterval
		Concept:		TBD
		Description:	"How many times the event should take place during one recurrence interval, for example, to specify 3-4 times per day, the CountPerInterval should be 3 to 4."
		0..1			MinCount
		0..1			MaxCount
		
				Element:	MinCount
				Description: "Lower bound on a count."
				Value:		integer   // FHIR - should be unsignedInt	
				
				Element:	MaxCount
				Description: "Upper bound on a count."
				Value:		integer // FHIR - should be unsignedInt		
		
	Element: 		RecurrenceRange
	Concept:		TBD
	Description:	"The start and end of the overall recurrence pattern in terms of dates/times or in terms of number of repeats. Could also be an event, such as when all doses are taken."
	Value:			TimePeriod or NumberOfRepeats
			
		Element:		NumberOfRepeats
		Concept:		TBD
		Description:	"How many times the action should be repeated."
		Value:			positiveInt



Element:	SampledData
Concept:	TBD
Description:	"Data that comes from a series of measurements taken by a device, which may have upper and lower limits. The data type also supports more than one dimension in the data."
Value:		string
1..1		Origin
1..1		MillisecondsBetweenSamples
0..1		CorrectionFactor
0..1		LowerLimit
0..1		UpperLimit
1..1		Dimensions

	Element:	Origin
	Description:	"The base quantity that a measured value of zero represents. In addition, this provides the units of the entire measurement series."
	Value:		SimpleQuantity
	
	Element:	MillisecondsBetweenSamples
	Description: "The length of time between sampling times, measured in milliseconds."
	Value:		decimal
	
	Element:	CorrectionFactor
	Description: "A correction factor that is applied to the sampled data points before they are added to the origin."
	Value:		decimal
	
	Element:	LowerLimit	
	Concept:	TBD
	Description:	"The lower limit of detection of the measured points. This is needed if any of the data points have the value 'L' (lower than detection limit)."
	Value:		decimal
	
	Element:		UpperLimit
	Concept:		TBD
	Description:	"The upper limit of detection of the measured points. This is needed if any of the data points have the value 'U' (higher than detection limit)."
	Value:			decimal
	 
	Element:	Dimensions
	Concept:	TBD
	Description:	"The number of sample points at each time point. If this value is greater than one, then the dimensions will be interlaced - all the sample points for a point in time will be recorded at once."
	Value:		positiveInt
	
Element:   		Address
Concept:		MTH#C0682130
Description:    "An address expressed using postal conventions (as opposed to GPS or other location definition formats). This data type may be used to convey addresses for use in delivering mail as well as for visiting locations and which might not be valid for mail delivery. There are a variety of postal address formats defined around the world. (Source: HL7 FHIR)."
0..1			Purpose from http://hl7.org/fhir/ValueSet/address-use
0..1			Type from http://hl7.org/fhir/ValueSet/address-type
0..1			DisplayText
0..2    		AddressLine
0..1    		City
0..1    		District
0..1    		State
0..1    		PostalCode
1..1    		Country
0..1			EffectiveTimePeriod

	Element:    	AddressLine
	Concept:		TBD
	Description:    "Part of an address that contains the house number, apartment number, street name, street direction, P.O. Box number, delivery hints, and similar address information. (Source: HL7 FHIR)."
	Value:         	string

	Element:		GeopoliticalLocation
	Concept:		MTH#C0017446
	Description:	"The countries of the world and major geopolitical subregions, such as US states."

	Element:    	City
	Based on:		GeopoliticalLocation
	Concept:		MTH#C1555315
	Description:    "The name of a municipality, city, town, village or other community or delivery center. (Source: HL7 FHIR)."
	Value:         	string

	Element:    	District
	Based on:		GeopoliticalLocation
	Concept:		MTH#C0079170
	Description:    "The name of the administrative area at a level below that of a state but above that of a city or town. In the US, a county. Outside the US, a district or the equivalent. (Source: HL7 FHIR)."
	Value:         	string

	Element:    	State
	Based on:		GeopoliticalLocation
	Concept:		MTH#C1552743
	Description:    "Sub-unit of a country with limited sovereignty in a federally organized country. A code may be used if codes are in common use (i.e. US 2 letter state codes). (Source: HL7 FHIR)."
	Value:         	string

	Element:    	PostalCode
	Concept:		MTH#C1514254
	Description:    "A sequence of letters and digits used as part of a postal address, often designating a geographic region"
	Value:         	string

	Element:    	Country
	Based on:		GeopoliticalLocation
	Concept:		MTH#C0454664
	Description:    "Country - a nation as commonly understood or generally accepted, expressed in ISO 3166 Alpha-2 (2-letter) codes."
	Value:         	string


/*	
//------------ Complex Data Types not in FHIR ----------------

Element:		Count
Concept:		MTH#C0750480
Description:	"The number of items (0 or more), as an integer."
Value:			unsignedInt

Element:		PlainText
Concept:		TBD
Description:	"A text written in a human language."
Value:			string
0..1			Language



Element:		ParsableContent
Based on:		EmbeddedContent
Concept:		TBD
Description:	"A string that follows a formal syntax and can be analyzed and broken into meaningful parts."
Value:			string
1..1			Formalism

	Element:		Formalism
	Concept:		TBD
	Description:	"The system of formal syntax or interpretation associated with parsable content."
	Value:			CodeableConcept

*  No can do! Can't restrict "value is type" to a primitive
Element:		UnsignedIntegerRange
Description:	"A range of non-negative integers. One of the two bounds must be present, and the lower bound must be less than or equal to the upper bound."
0..1			LowerBound value is type unsignedInt
0..1			UpperBound value is type unsignedInt
*


	


Element:		Percentage
Based on:		SimpleQuantity
Concept:		MTH#C0439165
Description:	"A percentage value where 100.0 represents 100%."
				Units.Coding is UCUM#%

Element:		Likelihood
Concept:		MTH#C0033204
Description:	"A measure of the expectation of the occurrence of a particular event, as a percentage."
Value:			positiveInt   // 0-100 do not allow zero, since this will create an implicit modifier

Element:		QualitativeLikelihood
Concept:		TBD
Description:	"A qualitative (subjective) likelihood."
Value:			CodeableConcept from QualitativeLikelihoodVS


//------------- Interpretations of Time -------------------



Element:		ReceivedTime
Concept:		MTH#C2735124	
Description:	"Time the item was delivered to, or accepted by, the receiving facility or unit."
Value:			dateTime



Element:		NonOccurrenceTimeOrPeriod
Concept:		TBD
Description:	"The point in time or span of time in which something did not happen."
Value:			date or dateTime or TimePeriod



Element:		OccurrencePeriod
Based on:		OccurrenceTimeOrPeriod
Concept:		TBD
Description:	"The point in time or span of time in which something happens."
Value:			TimePeriod

Element:		OccurrenceDuration
Concept:		TBD
Description:	"The length of time in which something happens."
Value:			Duration


//---------------  Locations and Addresses -----------------



Element:		Geoposition
Concept:		TBD
Description:	"The location on the surface of the Earth, described by a latitude and longitude (and optional altitude)."
1..1			Latitude
1..1			Longitude
0..1			Altitude

		Element:		Latitude
		Concept:		MTH#C1627936
		Description:	"The angular distance north or south between an imaginary line around a heavenly body parallel to its equator and the equator itself. Measured with with WGS84 datum."
		Value:			decimal

		Element:		Longitude
		Concept:		MTH#C1657623
		Description:	"An imaginary great circle on the surface of a heavenly body passing through the poles at right angles to the equator. Measured with with WGS84 datum."
		Value:			decimal

		Element:		Altitude
		Concept:		MTH#C0002349
		Description:	"Height above sea level or above the earth's surface. Measured with with WGS84 datum."
		Value:			decimal


Element:		Setting
Concept:		TBD
Description:	"Description of the place or type of surroundings where something is positioned or where an event takes place."
Value:			CodeableConcept from SettingVS


//------------  US Address Profile -----------

Element:    	UnitedStatesAddress
Concept:		TBD   // more specific concept than Address
Based on:		Address
Description:    "An address for a place in the USA, conforming to US mail postal conventions. (Source: HL7 FHIR)."
1..1			State is type UnitedStatesState
1..1			Country // is "US"   // fixed -- string constraints not yet supported

	Element:		UnitedStatesState
	Based on:		State
	Concept:		MTH#C3148680
	Description:	"A state or territory in the USA expressed by 2-letter US Postal code."
	//Value:			string from http://pe.usps.com/text/pub28/28apb.htm   // expressed as string to conform with definition of State


Element:   		HumanName
Based on:		PersonName
Concept: 		MTH#C1547383
Description:	"The CIMI person name, constrained to map correctly to FHIR."
0..0			MiddleName
0..1    		FamilyName
				Purpose from http://hl7.org/fhir/ValueSet/name-use
				
Element:		BodyPosition
Concept:		MTH#C1262869
Description:	"The position or physical attitude of the body."
Value:			CodeableConcept from BodyPositionVS

Element:		Details
Concept: 		MTH#C0947611
Description:	"A text note containing additional details, explanation, description, comment, or summarization. Details can discuss, support, explain changes to, or dispute information."
Value:			string  // change to language-text


Element:		ContactDetail
Concept:		TBD
Description:	"Describes an individual and how to reach them."
0..1			HumanName
0..*			ContactPoint



Element:		Category
Concept:		TBD
Description:	"A class or division of people or things having particular shared characteristics"
Value:			CodeableConcept

*/
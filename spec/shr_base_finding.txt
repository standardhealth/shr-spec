Grammar:		DataElement 5.0
Namespace:		shr.base
Description:	"Contains the SHR Entry definition -- the metadata added to any independent item in the SHR."
Uses:			shr.core, shr.entity, shr.encounter


Abstract Element:	NonIndependentFinding
Based on:			InformationItem
Description:		"A finding that is part of another finding, does not or cannot stand alone."
Value:			0..1 (Quantity or CodeableConcept or string or Range or Ratio or time or dateTime or TimePeriod)   
// exclude boolean because component doesn't include it, although Observation does; exclude Media because it is not present in FHIR v4
1..1			FindingTopicCode
0..1			ExceptionValue
0..1			ReferenceRange  // included here because components can have reference range which usually needs to be explicitly zeroed out. If it is not part of DataElement, it will appear in FHIR Profiles.

		Element:		FindingTopicCode
		Description:	"A code describing what the finding concerns, such as a lab test, property, or an item. In an observation, the code represents the 'question': the aspect or property of the Patient or SpecificFocusOfFinding being observed or measured. In an existence assertion, the code represents the type of event, condition, or phenomenon that is being asserted to exist or not exist. The topic code is not the result of the evaluation, which is found in the value. "
		Value:			CodeableConcept

		Element:		ExceptionValue
		Concept:		TBD
		Description:	"Reason that a value associated with a test or other finding is missing."
		Value:			CodeableConcept from http://hl7.org/fhir/ValueSet/data-absent-reason if covered

		Element:		ReferenceRange
		Concept:		MTH#C0883335
		Description:	"The usual or acceptable range for a test result."
		Value:			Range   // TODO: align this with FHIR
		0..1			Type from http://hl7.org/fhir/ValueSet/referencerange-meaning if covered
		0..1			ApplicableSubpopulation
		0..1			ApplicableAgeRange
		
				Element:	ApplicableSubpopulation
				Concept:	MTH#C0039309 "Target Population"
				Description:	"Codes to indicate the target population this reference range applies to. For example, a reference range may be based on the normal population or a particular sex or race."
				Value: 		CodeableConcept from http://hl7.org/fhir/ValueSet/referencerange-appliesto if covered
		
				Element:	ApplicableAgeRange
				Concept:	TBD
				Description:	"The age at which this reference range is applicable. This is a neonatal age (e.g. number of weeks at term) if the meaning says so."  // FHIR: how exactly does one indicate a neonatal or prenatal age?
				Value:		Range

Abstract Element:	Finding
Based on:		NonIndependentFinding // Although the parent is NonIndependentFinding, a Finding can be independent. It's a modeling compromise that allows better mapping. 
Description:	"An assertion, determination, opinion, evaluation, result, observation, or measurement made by a person or device."
0..1			Patient
0..1			SpecificFocusOfFinding
0..1			Encounter  // findings are often associated with specific encounters
0..1			FindingMethod
1..1			FindingStatus
0..1			RelevantTime
0..*			NonIndependentFinding

		Element:		SpecificFocusOfFinding
		Description:	"The specific observed item finding relates to. The SpecificFocusOfFinding is only required if the thing observed is different than the patient. The SpecificFocusOfFinding could be a relative of the patient, a location, or a group. The SpecificFocusOfFinding for cancer progression would be the cancer condition. If the property is wound length, the SpecificFocusOfFinding would be an instance of a wound. In the case of an assertion of existence, the SpecificFocusOfFinding is the larger entity that the asserted item is a portion of. For example, a wound tunnel is part of a wound; an organization part of a larger organization, or an encounter with a hospitalist might be part of a larger hospitalization encounter. SpecificFocusOfFinding should not be used to record anatomical location."
		Value:			string or CodeableConcept or Identifier or ref(InformationItem)
		0..1			RelationshipToPersonOfRecord

		Element:		RelevantTime
		Concept:		TBD
		Description:	"The time or time period that the statement addresses.
		The clinically relevant time is not necessarily when the information is gathered or when a test is carried out, but for example, when a specimen was collected, or the time period referred to by the question. Use a TimePeriod for a measurement or specimen collection continued over a significant period of time (e.g. 24 hour Urine Sodium)."
		Value:			dateTime or TimePeriod 

		Element:		FindingMethod
		Concept:		TBD
		Description:	"The technique used to create the finding, for example, the specific imaging technical, lab test code, or assessment vehicle."
		Value:			CodeableConcept should be from FindingMethodVS

		Element:		FindingStatus
		Concept:		TBD
		Description:	"Indicates whether the finding is preliminary, amended, final, etc."
		Value:			code or CodeableConcept

EntryElement:	Observation
Based on:		Finding
Concept:		TBD
Description:	"Represents the result of evaluations (measurements, tests, or questions) that have been performed.
Observation has a value representing the result (answer), or an ExceptionValue indicating why the value is not present. The SpecificFocusOfFinding can be the entire patient, or an entity such as a location body structure, intervention, or condition. Things observed about the subject can include social and behavioral factors, subjective and objective observations, and assessments.
For an Observation, the FindingTopicCode describes the aspect or property of the subject being observed or measured. The FindingTopicCode is the 'question code' that pairs to the 'answer' contained in the ResultValue."
//0..0			Observer
0..1			DeltaFlag
1..*			Category from http://hl7.org/fhir/ValueSet/observation-category if covered
0..1			AnatomicalLocation  // TODO: Change from element to code to align with FHIR??
0..1			AdditionalText	
0..1			Interpretation
0..1			Device
0..1			Specimen
0..*			Precondition
0..*			Media
				FindingStatus value is type code 
				FindingStatus from http://hl7.org/fhir/ValueSet/observation-status
0..1			PanelMembers

		Element:    	Observer
		Concept:        MTH#C0449416
		Description:    "The person or entity that provided the information in the entry, e.g. the subject (patient), medical professional, family member, device or software program, as distinct from who recorded the entry."
		Value:			ref(InformationItem) //string or ref(Practitioner) or ref(Patient) or ref(Group) or ref(Device) or ref(RelatedPerson) or ref(Organization)  
		0..1			RelationshipToPersonOfRecord

			Element:		RelationshipToPersonOfRecord
			Concept:		MTH#C0015608
			Description: 	"The relationship to the person of record."
			Value:			CodeableConcept from http://hl7.org/fhir/ValueSet/relatedperson-relationshiptype if covered

		Element:		AdditionalText
		Concept: 		MTH#C0947611
		Description:	"A text note containing additional details, explanation, description, comment, or summarization. Details can discuss, support, explain changes to, or dispute information."
		Value:			string  // change to language-text

		Element:		Interpretation
		Concept:		MTH#C0420833
		Description:	"A clinical interpretation of a finding (applies to both assertions and observation)."
		Value:			CodeableConcept from http://hl7.org/fhir/ValueSet/observation-interpretation if covered

		Element:		PanelMembers
		Concept:		TBD
		Description:	"PanelMembers represent the elements of a group of a related but independent evaluations. 
			Examples are the measurements that compose a complete blood count (CBC), or the elements of a pathology report. Each member is an independent evaluation, but the grouping reflects a composite lab order, shared specimen, or a single report author. Typically the Category and Reason are not given for individual findings that are part of the panel, but rather given at the level of the panel itself."
		0..* 			ref(Observation)

		Element:		Precondition
		Concept:		TBD
		Description:	"A description of the conditions or context of an observation, for example, under sedation, fasting or post-exercise. Body position and body site are also qualifiers, but handled separately. A qualifier cannot modify the measurement type; for example, a fasting blood sugar is still a blood sugar. "
		Value:			CodeableConcept or ref(Observation)
			
		Element:		DeltaFlag
		Concept:		MTH#C1705241
		Description:	"Indicator of significant change (delta) from the last or previous measurement."
		Value:			CodeableConcept from DeltaFlagVS
				
//		Element:		Issued
//		Description:	"The date and time this observation was made available to providers, typically after the results have been reviewed and verified."
//		Value:			instant


//  Non-Laboratory Results

EntryElement:	NonLaboratoryObservation
Based on:		Observation
Description:	"An observation not based on a specimen."
0..0			Specimen

EntryElement:	SimpleNonLaboratoryObservation
Based on:		NonLaboratoryObservation
Description:	"An observation having no components or panel members"
0..0			NonIndependentFinding
0..0			PanelMembers

EntryElement:	CodedNonLaboratoryObservation
Based on:		NonLaboratoryObservation
Concept:		TBD
Description:	"The result of evaluations (measurements, tests, or questions) whose answer is expressed as a code."
Value:			CodeableConcept
0..0			ReferenceRange

EntryElement:	SimpleCodedNonLaboratoryObservation
Based on:		CodedNonLaboratoryObservation
Description:	"An observation whose result is a code, and also having no components or panel members"
0..0			NonIndependentFinding
0..0			PanelMembers

EntryElement:	ComponentOnlyNonLaboratoryObservation
Based on:		NonLaboratoryObservation
Description:	"An observation with components, but without a result value or panel members."
0..0			Value
0..0			ExceptionValue
0..0			PanelMembers
0..0			ReferenceRange

// Laboratory Measurements and Results

EntryElement:	LaboratoryObservation
Based on:		Observation
Description:	"A coded finding based on a specimen collected from a patient."
1..1			Patient
1..1			Specimen
0..0			AnatomicalLocation  // AnatomicalLocation is determined indirectly from Specimen.CollectionSite
				Category is OBSCAT#laboratory
0..*			DiagnosticService

		Element:		DiagnosticService
		Concept:		TBD
		Description:	"The service completing the observations, e.g. biochemistry, microbiology, blood bank."
		Value:			CodeableConcept from http://hl7.org/fhir/ValueSet/diagnostic-service-sections if covered

EntryElement:	CodedLaboratoryObservation
Based on:		LaboratoryObservation
Description:	"A coded finding from a laboratory test."
Value:			CodeableConcept
0..0			ReferenceRange

EntryElement:	SimpleLaboratoryObservation
Based on:		LaboratoryObservation
Description:	"A coded finding from a laboratory test without further panel members or components."
0..0			NonIndependentFinding
0..0			PanelMembers

EntryElement:	SimpleCodedLaboratoryObservation
Based on:		CodedLaboratoryObservation
Description:	"A coded finding from a laboratory test without further panel members or components."
0..0			NonIndependentFinding
0..0			PanelMembers

//---------- Assertions -------------

Abstract Element:	ExistenceAssertion
Based on:			Finding
Description:		"An assertion of a relationship, situation, condition, or other phenomenon is present or absent, or an event has (or has not) taken place.
For assertions, the FindingTopicCode represents the thing asserted to be present or absent; i.e., the type of condition, event, relationship or phenomenon that is being asserted to exist or not exist."
Value:				CodeableConcept from PresentAbsentVS
0..0				ReferenceRange
0..1				ObjectIdentifier  // TODO: 0..* business identifier?
0..1				Certainty
					FindingStatus value is type CodeableConcept

		Element: 		ObjectIdentifier
		Description:	"For later reference, an identifier for the object implied by the assertion, which allows the object to be tracked over time. For example, a wound number is used to track a specific wound's condition over time."
		Value:			string

		Element:		Certainty   // TODO: why not move to Finding?
		Concept:		TBD
		Description:	"The degree of confidence in a conclusion or assertion."
		Value:			CodeableConcept

Abstract Element:	PresenceAssertion
Based on:			ExistenceAssertion
Description:		"Finding that a phenomena is present."
					Value is SCT#52101004 "Present (qualifier value"			
0..1				Certainty from PresenceCertaintyVS


Abstract Element:	AbsenceAssertion
Based on:			ExistenceAssertion
Description:		"Finding that an entity, person, role, condition, or phenomenon is absent."
					Value is SCT#2667000 "Absent (qualifier value)"	
0..1				Certainty from AbsenceCertaintyVS


EntryElement:	ConditionPresentAssertion
Based on:		PresenceAssertion
Concept:		TBD
Description:	"A condition that is or may be present in a subject. 'Condition' is interpreted broadly and could be a disorder, abnormality, problem, injury, complaint, functionality, illness, disease, ailment, sickness, affliction, upset, difficulty, disorder, symptom, worry, or trouble."
//0..0			SourceOfInformation //value is type PatientOrPractitionerOrRelatedPerson
				FindingTopicCode from http://hl7.org/fhir/us/core/ValueSet/us-core-problem if covered  // cannot make binding weaker because US Core requires an extensible binding
1..*			Category from ConditionCategoryVS
0..*			AnatomicalLocation
0..*			ConditionCause
1..1			ClinicalStatus
0..1			WhenClinicallyRecognized
0..1			PresentOnAdmission
0..1			Severity
0..1			Criticality
0..1			Stage
0..1			Onset
0..1			Abatement   // TODO: change abatement to an Observation

			Element:		ConditionCause
			Description: "The cause of the condition. This includes such causes as disease vectors and endogenous states (e.g., macular degeneration due to diabetes), substances, medications, micro-organisms, and forces associated with the occurrence of a Condition from temporal or causative perspectives"
			Value:			CodeableConcept or ref(Entity) or ref(Finding)
			// probably other attributes needed, such as likelihood or type of cause

			Element:		ClinicalStatus
			Concept:		TBD
			Description:	"A flag indicating whether the condition is active or inactive, recurring, in remission, or resolved (as of the last update of the Condition)."
			Value:			code from http://hl7.org/fhir/ValueSet/condition-clinical

			Element:		WhenClinicallyRecognized
			Concept:		TBD
			Description:	"The time at which a condition or condition was first identified in a healthcare context."
			Value:			date

			Element:		PresentOnAdmission
			Concept:		LNC#89251-3 "Condition present on admission"
			Description:	"If the problem or condition existed before the current episode of care."
			Value:			CodeableConcept from YesNoUnknownVS

			Element:		Severity
			Concept:		MTH#C0392364
			Description:	"Degree of harshness or extent of a symptom, disorder, or condition."
			Value: 			CodeableConcept from http://hl7.org/fhir/ValueSet/condition-severity

			Element:		Criticality
			Concept:		MTH#C3858539 "Criticality"
			Description:	"The potential clinical harm associated with a condition. When the worst case result is assessed to have a life-threatening or organ system threatening potential, it is considered to be of high criticality."
			Value:			code from http://hl7.org/fhir/ValueSet/allergy-intolerance-criticality

			Element:		Stage
			Concept:		MTH#C0699749
			Description:	"The relative advancement in the course of a disease. The value is the summary stage or stage group. StageDetail optionally contains the full staging information."
			Value:			0..1 CodeableConcept
			0..1			StageDetail
			
					Element:		StageDetail
					Concept:		TBD
					Description:	"The full staging information"
					Value:			ref(Observation)

			Element:		AlleviatingFactor
			Concept:		LNC#55258-8
			Description:	"Activities that reduce the severity of the pathology"
			Value:			CodeableConcept

			Element:		ExacerbatingFactor
			Concept:		LNC#55258-8
			Description:	"Activities that increase the severity of the pathology"
			Value:			CodeableConcept

			Element:		Onset
			Concept:		LNC#85585-8
			Description:	"The beginning or first appearance of a mental or physical disorder."
			Value:			dateTime or Age or TimePeriod or Range or string
			
			Element:		Abatement
			Concept:		LNC#88878-4
			Description:	"The end, remission or resolution."
			Value:			dateTime or Age or boolean or TimePeriod or Range or string


EntryElement:	ConditionAbsentAssertion
Based on:		AbsenceAssertion
Concept:		MTH#C0277541  "no disease present"
Description:	"A finding that a condition is or was not present in the subject at a certain time or time period, not necessarily the time the information is gathered."
				FindingTopicCode from http://hl7.org/fhir/us/core/ValueSet/us-core-problem if covered
1..*			Category from ConditionCategoryVS
0..*			AnatomicalLocation
				
EntryElement:	ClinicalNote
Based on:		Finding
Concept:		MTH#C0747978 "Progress Note"
Description:	"An entry concerning a patient where the result is a narrative text."
1..1			Patient
1..1			AdditionalText // make required

Abstract Element:	RelationshipExistenceAssertion
Based on:			ExistenceAssertion
Description:		"An assertion of a realtionship between two things being either present or absent. The subject of the relationship is is the Patient (or SpecificFocusOfFinding, if given) and the object of the relationship is .
For relationships, FindingTopicCode describes the relationship, e.g., Father or Mother."
Value:				CodeableConcept from PresentAbsentVS
